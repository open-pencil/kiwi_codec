defmodule KiwiCodec.RustlerGenerator do
  @moduledoc """
  Generates Rustler decoder code from Kiwi schemas for RustQ manifests.

  This is an experimental bridge for optional native backends. It returns
  RustQ splice replacements for schema-dependent decoder functions and NIF
  entrypoints; `rustq.exs` owns rendering and writing generated files.

  Generated Rust expects the template to provide these imports and helpers:

  * `rustler::{Binary, Encoder, Env, Error, NifResult, Term}`
  * `rustler::types::atom::Atom`
  * `std::sync::OnceLock`
  * a `Decoder<'a>` type with Kiwi primitive reader methods
  * the `__rq_rustler_helpers!();` splice emitted by `splices/2`

  The helper splice provides cached atoms, cached struct keys, default struct
  values, and raw `NIF_TERM` struct map construction. See the README for a
  minimal RustQ template skeleton.
  """

  alias KiwiCodec.RustlerGenerator.Name
  alias KiwiCodec.RustlerGenerator.Selection
  alias KiwiCodec.RustlerGenerator.Splice
  alias KiwiCodec.Schema
  alias KiwiCodec.Schema.Definition
  alias RustQ.Meta.AST, as: MetaAST
  alias RustQ.Rust
  alias RustQ.Rust.AST.Builder, as: A

  @primitive_decoders [
    {"bool", :read_bool, []},
    {"byte", :read_byte, []},
    {"float", :read_var_float, [:env]},
    {"int", :read_var_int, []},
    {"int64", :read_var_int64, []},
    {"string", :read_string, [:env]},
    {"uint", :read_var_uint, []},
    {"uint64", :read_var_uint64, []}
  ]

  for {type, method, args} <- @primitive_decoders do
    call =
      quote do
        decoder.unquote(method)(unquote_splicing(Enum.map(args, &Macro.var(&1, nil))))
      end

    defp primitive_decoder_expr(%{type: unquote(type)}) do
      unquote(Macro.escape(call))
    end
  end

  defp primitive_decoder_expr(_field), do: nil

  @type entrypoint :: {atom() | String.t(), String.t()}

  @doc """
  Returns RustQ splice replacements for a schema.

  Use this from `rustq.exs` with `render/2`. The template must contain
  `__rq_rustler_helpers!();`, `__rq_definitions!();`, and, when NIF entrypoints
  are requested, `__rq_entrypoints!();` splice anchors:

      generate :native_decoders, "native/my_nif/src/generated.rs" do
        schema = KiwiCodec.parse_schema!(File.read!("priv/schema.kiwi"))

        render File.read!("native/my_nif/src/generated.template.rs"),
          filename: "native/my_nif/src/generated.template.rs",
          splice: KiwiCodec.RustlerGenerator.splices(schema,
            definitions: ["Node"],
            entrypoints: [decode_node: "Node"],
            module_prefix: "Example.Schema"
          )
      end

  """
  @spec splices(Schema.t(), keyword()) :: [{atom(), [RustQ.Rust.Fragment.t()]}]
  def splices(%Schema{} = schema, opts) do
    definitions = Keyword.get(opts, :definitions, [])
    entrypoints = Keyword.get(opts, :entrypoints, [])
    module_prefix = Keyword.fetch!(opts, :module_prefix)

    definition_map = Selection.definition_map(schema)
    selected = Selection.definitions(schema, definitions, definition_map)

    [
      {:rustler_helpers, Splice.rustler_helpers()},
      {:definitions, definition_fragments(selected, module_prefix, definition_map)},
      {:entrypoints, entrypoint_fragments(entrypoints)}
    ] ++ Keyword.get(opts, :extra_splices, [])
  end

  defp definition_fragments(definitions, module_prefix, definition_map) do
    definitions
    |> Enum.flat_map(&definition_items(&1, module_prefix, definition_map))
    |> Enum.map(&fragment_code/1)
  end

  defp definition_items(%Definition{kind: :enum} = definition, _module_prefix, _definition_map) do
    variant_statics =
      definition.fields
      |> Enum.with_index()
      |> Enum.map(fn {_field, index} ->
        atom_static(Name.enum_variant_atom_static(definition.name, index))
      end)

    variant_statics ++ [enum_decoder_item(definition)]
  end

  defp definition_items(%Definition{kind: :struct} = definition, module_prefix, definition_map) do
    [
      atom_static(Name.module_atom_static(definition.name)),
      keys_static(Name.struct_keys_static(definition.name)),
      struct_decoder_item(definition, module_prefix, definition_map)
    ]
  end

  defp definition_items(%Definition{kind: :message} = definition, module_prefix, definition_map) do
    [
      atom_static(Name.module_atom_static(definition.name)),
      keys_static(Name.struct_keys_static(definition.name)),
      message_decoder_item(definition, module_prefix, definition_map),
      message_fields_decoder_item(definition, module_prefix, definition_map)
    ]
  end

  defp atom_static(name) do
    Rust.ast_item(A.static(name, "OnceLock<Atom>", A.path_call([:OnceLock, :new])))
  end

  defp keys_static(name) do
    Rust.ast_item(
      A.static(name, "OnceLock<Vec<rustler::wrapper::NIF_TERM>>", A.path_call([:OnceLock, :new]))
    )
  end

  defp enum_decoder_item(%Definition{} = definition) do
    definition
    |> generated_enum_module!()
    |> MetaAST.item(Name.decoder_function(definition.name))
  end

  defp generated_enum_module!(%Definition{} = definition) do
    module =
      Module.concat([
        KiwiCodec.RustlerGenerator.Generated,
        "Enum#{definition.name}#{:erlang.phash2(definition)}"
      ])

    if Code.ensure_loaded?(module) do
      module
    else
      variants =
        definition.fields
        |> Enum.with_index()
        |> Enum.map(fn {field, index} ->
          {
            field.value,
            Name.static_alias(Name.enum_variant_atom_static(definition.name, index)),
            Name.field_name(field.name)
          }
        end)

      name = Name.decoder_function(definition.name)

      Module.create(
        module,
        quote do
          use RustQ.Meta
          alias RustQ.Type, as: R
          import KiwiCodec.RustlerGenerator.DecoderMacro, only: [enum_decoder: 2]

          enum_decoder(
            unquote(name),
            unquote(Macro.escape(variants))
          )
        end,
        Macro.Env.location(__ENV__)
      )

      module
    end
  end

  defp struct_decoder_item(%Definition{} = definition, module_prefix, definition_map) do
    definition
    |> generated_struct_module!(module_prefix, definition_map)
    |> MetaAST.item(Name.decoder_function(definition.name))
  end

  defp generated_struct_module!(%Definition{} = definition, module_prefix, definition_map) do
    module =
      Module.concat([
        KiwiCodec.RustlerGenerator.Generated,
        "Struct#{definition.name}#{:erlang.phash2({definition, module_prefix})}"
      ])

    if Code.ensure_loaded?(module) do
      module
    else
      name = Name.decoder_function(definition.name)
      field_exprs = Enum.map(definition.fields, &field_value_expr(&1, definition_map))

      Module.create(
        module,
        quote do
          use RustQ.Meta
          alias RustQ.Type, as: R
          import KiwiCodec.RustlerGenerator.DecoderMacro, only: [struct_decoder: 6]

          struct_decoder(
            unquote(name),
            unquote(Name.module_atom_static(definition.name)),
            unquote(Name.struct_keys_static(definition.name)),
            unquote(Name.module_name(module_prefix, definition.name)),
            unquote(Enum.map(definition.fields, &Name.field_name(&1.name))),
            unquote(Macro.escape(field_exprs))
          )
        end,
        Macro.Env.location(__ENV__)
      )

      module
    end
  end

  defp message_decoder_item(%Definition{} = definition, module_prefix, definition_map) do
    definition
    |> generated_message_module!(module_prefix, definition_map)
    |> MetaAST.item(Name.decoder_function(definition.name))
  end

  defp message_fields_decoder_item(%Definition{} = definition, module_prefix, definition_map) do
    definition
    |> generated_message_module!(module_prefix, definition_map)
    |> MetaAST.item(Name.message_fields_function(definition.name))
  end

  defp generated_message_module!(%Definition{} = definition, module_prefix, definition_map) do
    module =
      Module.concat([
        KiwiCodec.RustlerGenerator.Generated,
        "Message#{definition.name}#{:erlang.phash2({definition, module_prefix})}"
      ])

    if Code.ensure_loaded?(module) do
      module
    else
      decoder_name = Name.decoder_function(definition.name)
      fields_name = Name.message_fields_function(definition.name)

      fields =
        definition.fields
        |> Enum.with_index()
        |> Enum.map(fn {field, index} ->
          {field.value, index + 1, field_value_expr(field, definition_map)}
        end)

      module_name = Name.module_name(module_prefix, definition.name)

      Module.create(
        module,
        quote do
          use RustQ.Meta
          alias RustQ.Type, as: R

          import KiwiCodec.RustlerGenerator.DecoderMacro,
            only: [message_decoder: 6, message_fields_decoder: 2]

          message_decoder(
            unquote(decoder_name),
            unquote(fields_name),
            unquote(Name.module_atom_static(definition.name)),
            unquote(Name.struct_keys_static(definition.name)),
            unquote(module_name),
            unquote(Enum.map(definition.fields, &Name.field_name(&1.name)))
          )

          message_fields_decoder(unquote(fields_name), unquote(Macro.escape(fields)))
        end,
        Macro.Env.location(__ENV__)
      )

      module
    end
  end

  defp field_value_expr(%{array?: true, type: "byte"}, _definition_map) do
    quote(do: decoder.read_byte_array(env))
  end

  defp field_value_expr(%{array?: true} = field, definition_map) do
    inner = field_result_expr(%{field | array?: false}, definition_map)

    quote do
      decoder.read_repeated(fn decoder -> unquote(inner) end)
    end
  end

  defp field_value_expr(field, definition_map), do: field_result_expr(field, definition_map)

  defp field_result_expr(field, definition_map) do
    primitive_decoder_expr(field) ||
      field
      |> referenced_definition!(definition_map)
      |> then(fn definition ->
        name = Name.decoder_function(definition.name)

        quote do
          unquote(name)(env, decoder)
        end
      end)
  end

  defp referenced_definition!(field, definition_map), do: Map.fetch!(definition_map, field.type)

  defp entrypoint_fragments(entrypoints) do
    Enum.map(entrypoints, fn {nif_name, definition_name} ->
      entrypoint_item(nif_name, definition_name)
    end)
  end

  defp entrypoint_item(nif_name, definition_name) do
    module = generated_entrypoint_module!(nif_name, definition_name)
    MetaAST.item(module, RustQ.Atom.identifier!(to_string(nif_name)))
  end

  defp generated_entrypoint_module!(nif_name, definition_name) do
    nif_name = RustQ.Atom.identifier!(to_string(nif_name))
    decoder_name = Name.decoder_function(definition_name)

    module =
      Module.concat([
        KiwiCodec.RustlerGenerator.Generated,
        "Entrypoint#{nif_name}#{:erlang.phash2(definition_name)}"
      ])

    if Code.ensure_loaded?(module) do
      module
    else
      Module.create(
        module,
        quote do
          use RustQ.Meta
          alias RustQ.Type, as: R
          import KiwiCodec.RustlerGenerator.DecoderMacro, only: [entrypoint: 2]

          entrypoint(unquote(nif_name), unquote(decoder_name))
        end,
        Macro.Env.location(__ENV__)
      )

      module
    end
  end

  defp fragment_code(fragment), do: RustQ.Rust.to_fragment(fragment)
end
