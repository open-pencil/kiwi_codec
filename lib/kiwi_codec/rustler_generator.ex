defmodule KiwiCodec.RustlerGenerator do
  @moduledoc """
  Generates Rustler decoder code from Kiwi schemas using Rust templates.

  This is an experimental bridge for optional native backends. It keeps Rust
  runtime code in real Rust templates and only generates schema-dependent
  decoder functions and NIF entrypoints.
  """

  alias KiwiCodec.Schema
  alias KiwiCodec.Schema.Definition
  alias RustQ.Rust
  alias RustQ.Rust.AST
  alias RustQ.Rust.AST.Builder, as: A
  alias RustQ.Rust.AST.PatternBuilder, as: P

  require A

  @primitive_decoders [
    {"bool", :read_bool, []},
    {"byte", :read_byte, []},
    {"float", :read_var_float, [:env]},
    {"int", :read_var_int, []},
    {"int64", :read_var_int64, []},
    {"string", :read_string, [:env]},
    {"uint", :read_var_uint, []},
    {"uint64", :read_var_uint64, []}
  ]

  for {type, method, args} <- @primitive_decoders do
    defp primitive_decoder_expr(%{type: unquote(type)}) do
      A.method(:decoder, unquote(method), unquote(args))
    end

    defp primitive_decoder_source(%{type: unquote(type)}) do
      unquote("decoder.#{method}(#{Enum.map_join(args, ", ", &to_string/1)})")
    end
  end

  defp primitive_decoder_expr(_field), do: nil
  defp primitive_decoder_source(_field), do: nil

  @type entrypoint :: {atom() | String.t(), String.t()}

  @doc """
  Renders a Rust template with generated Kiwi decoder replacements.

  Generates native decoders for enums, structs, and messages.
  """
  @spec render!(Schema.t(), keyword()) :: Path.t()
  def render!(%Schema{} = schema, opts) do
    template = Keyword.fetch!(opts, :template)
    out = Keyword.fetch!(opts, :out)

    KiwiCodec.RustTemplate.render!(template, out, replacements(schema, opts), opts)
  end

  @doc """
  Renders a Rust template with generated Kiwi decoder replacements and returns source.

  Accepts the same options as `render!/2`, except `:out` is not required.
  """
  @spec render_source!(Schema.t(), keyword()) :: String.t()
  def render_source!(%Schema{} = schema, opts) do
    template = Keyword.fetch!(opts, :template)

    KiwiCodec.RustTemplate.render_source!(template, replacements(schema, opts), opts)
  end

  defp replacements(%Schema{} = schema, opts) do
    definitions = Keyword.get(opts, :definitions, [])
    entrypoints = Keyword.get(opts, :entrypoints, [])
    module_prefix = Keyword.fetch!(opts, :module_prefix)

    definition_map = Map.new(schema.definitions, &{&1.name, &1})
    selected = select_definitions(schema, definitions, definition_map)

    [
      {:definitions, definition_fragments(selected, module_prefix, definition_map)},
      {:entrypoints, entrypoint_fragments(entrypoints)}
    ] ++ Keyword.get(opts, :extra_splices, [])
  end

  defp select_definitions(%Schema{} = schema, [], _definition_map), do: schema.definitions

  defp select_definitions(%Schema{} = schema, names, definition_map) do
    names
    |> Enum.map(&to_string/1)
    |> include_dependencies(definition_map, MapSet.new())
    |> then(fn selected_names ->
      Enum.filter(schema.definitions, &MapSet.member?(selected_names, &1.name))
    end)
  end

  defp include_dependencies([], _definition_map, acc), do: acc

  defp include_dependencies([name | names], definition_map, acc) do
    if MapSet.member?(acc, name) do
      include_dependencies(names, definition_map, acc)
    else
      definition = Map.fetch!(definition_map, name)

      dependencies =
        definition.fields
        |> Enum.map(& &1.type)
        |> Enum.filter(&Map.has_key?(definition_map, &1))

      include_dependencies(names ++ dependencies, definition_map, MapSet.put(acc, name))
    end
  end

  defp definition_fragments(definitions, module_prefix, definition_map) do
    definitions
    |> Enum.flat_map(&definition_items(&1, module_prefix, definition_map))
    |> Enum.map(&fragment_code/1)
  end

  defp definition_items(%Definition{kind: :enum} = definition, _module_prefix, _definition_map) do
    variant_statics =
      definition.fields
      |> Enum.with_index()
      |> Enum.map(fn {_field, index} ->
        atom_static(enum_variant_atom_static(definition.name, index))
      end)

    variant_statics ++ [Rust.ast_item(enum_decoder_ast(definition))]
  end

  defp definition_items(%Definition{kind: :struct} = definition, module_prefix, definition_map) do
    [
      atom_static(module_atom_static(definition.name)),
      keys_static(struct_keys_static(definition.name)),
      Rust.ast_item(struct_decoder_ast(definition, module_prefix, definition_map))
    ]
  end

  defp definition_items(%Definition{} = definition, module_prefix, definition_map) do
    definition
    |> definition_code(module_prefix, definition_map)
    |> rust_item_fragment()
    |> List.wrap()
  end

  defp definition_code(%Definition{kind: :message} = definition, module_prefix, definition_map) do
    fields =
      definition.fields |> Enum.with_index() |> Enum.map(&message_field_arm(&1, definition_map))

    """
    fn #{decoder_name(definition.name)}_from_decoder<'a>(env: Env<'a>, decoder: &mut Decoder<'_>) -> NifResult<Term<'a>> {
        static MODULE_ATOM: OnceLock<Atom> = OnceLock::new();
        static STRUCT_KEYS: OnceLock<Vec<rustler::wrapper::NIF_TERM>> = OnceLock::new();
        let module_atom = cached_atom(env, &MODULE_ATOM, #{rust_string(module_name(module_prefix, definition.name))});
        let keys = cached_struct_keys(env, &STRUCT_KEYS, &[#{field_names(definition.fields)}]);
        let mut values = default_values(module_atom, keys.len() - 1);
        loop {
            match decoder.read_var_uint()? {
                0 => break,
    #{indent(fields, 12)}
                field => return Err(Error::Term(Box::new(format!("unknown field {} while decoding #{definition.name}", field)))),
            }
        }
        make_struct(env, keys, &values)
    }
    """
  end

  defp atom_static(name) do
    Rust.ast_item(A.static(name, "OnceLock<Atom>", A.path_call([:OnceLock, :new])))
  end

  defp keys_static(name) do
    Rust.ast_item(
      A.static(name, "OnceLock<Vec<rustler::wrapper::NIF_TERM>>", A.path_call([:OnceLock, :new]))
    )
  end

  defp enum_decoder_ast(%Definition{} = definition) do
    %AST.Function{
      name: decoder_function_name(definition.name),
      lifetime: :a,
      args: decoder_args(),
      returns: term_result_type(),
      body: [
        A.return(
          A.match_expr(
            A.cast(A.try(A.method(:decoder, :read_var_uint)), A.type_path(:i64)),
            enum_arms(definition) ++ [unknown_enum_arm()]
          )
        )
      ]
    }
  end

  defp enum_arms(%Definition{} = definition) do
    definition.fields
    |> Enum.with_index()
    |> Enum.map(fn {field, index} ->
      atom_static = enum_variant_atom_static(definition.name, index)

      %AST.Arm{
        pattern: P.lit(field.value),
        body: [
          A.return(
            A.ok(
              A.method(
                A.call(:cached_atom, [
                  :env,
                  A.ref(atom_static),
                  field.name |> field_name() |> A.lit()
                ]),
                :encode,
                [:env]
              )
            )
          )
        ]
      }
    end)
  end

  defp unknown_enum_arm do
    %AST.Arm{
      pattern: P.var(:value),
      body: [A.return(A.ok(A.method(:value, :encode, [:env])))]
    }
  end

  defp struct_decoder_ast(%Definition{} = definition, module_prefix, definition_map) do
    module_atom = module_atom_static(definition.name)
    struct_keys = struct_keys_static(definition.name)

    %AST.Function{
      name: decoder_function_name(definition.name),
      lifetime: :a,
      args: decoder_args(),
      returns: term_result_type(),
      body:
        [
          A.let(
            :module_atom,
            A.call(:cached_atom, [
              :env,
              A.ref(module_atom),
              A.lit(module_name(module_prefix, definition.name))
            ])
          ),
          A.let(
            :keys,
            A.call(:cached_struct_keys, [
              :env,
              A.ref(struct_keys),
              A.ref(A.array(Enum.map(definition.fields, &A.lit(field_name(&1.name)))))
            ])
          ),
          A.let_mut(:values, A.path_call([:Vec, :with_capacity], [A.method(:keys, :len)])),
          A.stmt(A.method(:values, :push, [A.method(:module_atom, :as_c_arg)]))
        ] ++
          Enum.flat_map(definition.fields, &struct_field_stmts(&1, definition_map)) ++
          [A.return(A.call(:make_struct, [:env, :keys, A.ref(:values)]))]
    }
  end

  defp struct_field_stmts(field, definition_map) do
    [
      A.let(:value, field_value_expr(field, definition_map)),
      A.stmt(A.method(:values, :push, [A.method(A.method(:value, :encode, [:env]), :as_c_arg)]))
    ]
  end

  defp message_field_arm({field, index}, definition_map) do
    value = field_value_source(field, definition_map)

    """
    #{field.value} => {
        let value = #{value};
        values[#{index + 1}] = value.encode(env).as_c_arg();
    }
    """
  end

  defp field_value_expr(%{array?: true, type: "byte"}, _definition_map) do
    A.try(A.method(:decoder, :read_byte_array, [:env]))
  end

  defp field_value_expr(%{array?: true} = field, definition_map) do
    A.try(
      A.method(:decoder, :read_repeated, [
        A.closure([:decoder], field_result_expr(%{field | array?: false}, definition_map))
      ])
    )
  end

  defp field_value_expr(field, definition_map),
    do: A.try(field_result_expr(field, definition_map))

  defp field_result_expr(field, definition_map) do
    primitive_decoder_expr(field) ||
      field
      |> referenced_definition!(definition_map)
      |> then(&A.call(decoder_function_name(&1.name), [:env, :decoder]))
  end

  defp field_value_source(%{array?: true, type: "byte"}, _definition_map) do
    "decoder.read_byte_array(env)?"
  end

  defp field_value_source(%{array?: true} = field, definition_map) do
    "decoder.read_repeated(|decoder| #{field_result_source(%{field | array?: false}, definition_map)})?"
  end

  defp field_value_source(field, definition_map),
    do: "#{field_result_source(field, definition_map)}?"

  defp field_result_source(field, definition_map) do
    primitive_decoder_source(field) ||
      field
      |> referenced_definition!(definition_map)
      |> then(&"#{decoder_name(&1.name)}_from_decoder(env, decoder)")
  end

  defp referenced_definition!(field, definition_map), do: Map.fetch!(definition_map, field.type)

  defp entrypoint_fragments(entrypoints) do
    Enum.map(entrypoints, fn {nif_name, definition_name} ->
      definition_name
      |> entrypoint_ast(nif_name)
      |> Rust.ast_item()
    end)
  end

  defp entrypoint_ast(definition_name, nif_name) do
    decoder_name = decoder_function_name(definition_name)

    %AST.Function{
      name: RustQ.Atom.identifier!(to_string(nif_name)),
      vis: :pub,
      lifetime: :a,
      attrs: [A.nif_attr(schedule: "DirtyCpu")],
      args: [
        A.arg(:env, A.type_path(:Env, lifetimes: [:a])),
        A.arg(:bytes, A.type_path(:Binary, lifetimes: [:a]))
      ],
      returns: A.type_path(:NifResult, generics: [A.type_path(:Term, lifetimes: [:a])]),
      body: [
        A.let_mut(:decoder, A.path_call([:Decoder, :new], [A.method(:bytes, :as_slice)])),
        A.let(:term, A.try(A.call(decoder_name, [:env, A.mut_ref(:decoder)]))),
        A.stmt(A.try(A.method(:decoder, :finish))),
        A.return(A.ok(:term))
      ]
    }
  end

  defp rust_item_fragment(code) do
    RustQ.parse_fragment!(:item, code)
  end

  defp fragment_code(fragment) do
    RustQ.Rust.to_fragment(fragment)
  end

  defp decoder_name(name), do: "decode_#{rust_ident(name)}"

  defp decoder_function_name(name),
    do: name |> decoder_name() |> Kernel.<>("_from_decoder") |> RustQ.Atom.identifier!()

  defp decoder_args do
    [
      A.arg(:env, A.type_path(:Env, lifetimes: [:a])),
      A.arg(:decoder, "&mut Decoder<'_>")
    ]
  end

  defp term_result_type do
    A.type_path(:NifResult, generics: [A.type_path(:Term, lifetimes: [:a])])
  end

  defp module_atom_static(name), do: static_name(name, "MODULE_ATOM")
  defp struct_keys_static(name), do: static_name(name, "STRUCT_KEYS")
  defp enum_variant_atom_static(name, index), do: static_name(name, "ATOM_#{index}")

  defp static_name(name, suffix) do
    name
    |> rust_ident()
    |> String.upcase()
    |> Kernel.<>("_#{suffix}")
    |> RustQ.Atom.identifier!()
  end

  defp module_name(module_prefix, name) do
    "Elixir.#{module_prefix}.#{name}"
  end

  defp field_name(name), do: Macro.underscore(name)

  defp field_names(fields) do
    Enum.map_join(fields, ", ", fn field -> field.name |> field_name() |> rust_string() end)
  end

  defp rust_ident(name) do
    name
    |> Macro.underscore()
    |> String.replace(~r/[^a-zA-Z0-9_]/, "_")
  end

  defp rust_string(value) do
    escaped =
      value
      |> String.replace("\\", "\\\\")
      |> String.replace("\"", "\\\"")

    "\"#{escaped}\""
  end

  defp indent(lines, spaces) when is_list(lines) do
    lines |> Enum.join("\n") |> indent(spaces)
  end

  defp indent(text, spaces) do
    padding = String.duplicate(" ", spaces)

    text
    |> String.trim_trailing()
    |> String.split("\n")
    |> Enum.map_join("\n", &(padding <> &1))
  end
end
